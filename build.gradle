plugins {
    id "fabric-loom" version "${loom_version}"
    id "maven-publish"
}

version = "${project.mod_version}+${project.minecraft_version}"
group = project.maven_group

repositories {
    maven {
        name = "Mod Menu"
        url = "https://maven.terraformersmc.com/releases/"
    }
    maven {
        name = "DevLogin"
        url = "https://maven.covers1624.net/"
    }
    maven {
        name = "henkelmax.public"
        url = "https://maven.maxhenkel.de/repository/public"
    }
    maven {
        name = "Resourceful Config"
        url = "https://maven.teamresourceful.com/repository/maven-public/"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.layered {
        mappings("net.fabricmc:yarn:${project.yarn_mappings}:v2")
    }
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    // Mod Menu
    modImplementation("com.terraformersmc:modmenu:${project.modmenu_version}")
    // Resourceful Config
    modImplementation "com.teamresourceful.resourcefulconfig:resourcefulconfig-fabric-${project.rconfig_version}"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.8.0"
    include "org.jetbrains.kotlin:kotlin-stdlib:1.8.0"

    implementation "io.socket:socket.io-client:2.1.1"
    include "io.socket:socket.io-client:2.1.1"
    include "io.socket:engine.io-client:2.1.0"
    include "com.squareup.okhttp3:okhttp:3.12.12"
    include "org.json:json:20231013"
    include "com.squareup.okio:okio:3.4.0"

    implementation "de.maxhenkel.opus4j:opus4j:2.0.2"
    include "de.maxhenkel.opus4j:opus4j:2.0.2"

    // Dev Login
    localRuntime "net.covers1624:DevLogin:0.1.0.5"
}

loom {
    accessWidenerPath = file("src/main/resources/mineboxadditions.accesswidener")
    runs {
        clientLogin {
            client()
            ideConfigGenerated true
            programArgs "--launch_target", "net.fabricmc.loader.impl.launch.knot.KnotClient"
            mainClass.set "net.covers1624.devlogin.DevLogin"
        }
    }
}

base {
    archivesName = project.archives_base_name
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version

    filesMatching("fabric.mod.json") {
        expand "version": inputs.properties.version,
                "minecraft_version": inputs.properties.minecraft_version,
                "loader_version": inputs.properties.loader_version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

jar {
    from("LICENSE") {
        rename { "${it}_${base.archivesName.get()}" }
    }
}